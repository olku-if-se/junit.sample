plugins {
    id 'java'
    id 'jacoco'
    id 'org.gosu-lang.gosu' version '+'
    id 'idea'
}

// helper scripts
apply from: "$rootDir/gradle/task-graph-publisher.gradle"
apply from: "$rootDir/gradle/offline-local-maven-repo.gradle"
apply from: "$rootDir/gradle/test-custom-terminal-reporter.gradle"
apply from: "$rootDir/gradle/helpers.gradle"

println "âœ… Using local Maven repository at: $localRepoPath"

repositories {
    maven { url project.file(localRepoPath) }
    mavenCentral()
}

group = 'org.example'
version = '1.0-SNAPSHOT'
description = 'Gosu Junit Gradle Project Example'

// force Java 11 for project code (stay close to Guidewire environment)
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
        //vendor.set(JvmVendorSpec.AMAZON)
    }
    targetCompatibility = 11
    sourceCompatibility = 11
}
compileJava.options.release = 11

// Include Gosu source sets Into Compilation Process
sourceSets {
    main {
        gosu {
            srcDirs = ['src/main/gosu']
            filter.include '**/*.gs', '**/*.gsx', '**/*.gst', '**/*.gsp'
            filter.excludes = []
        }
    }
    test {
        gosu {
            srcDirs = ['src/test/gosu']
            filter.include '**/*.gs', '**/*.gsx', '**/*.gst', '**/*.gsp'
            filter.excludes = []
        }
    }
}

dependencies {
    // Gosu depends on Manifold
    implementation 'systems.manifold:manifold:+'

    // Gosu Language dependencies
    implementation('org.gosu-lang.gosu:gosu-core:+')
    testImplementation('org.gosu-lang.gosu:gosu-test:+')

    // JUnit 5 dependencies
    testImplementation platform('org.junit:junit-bom:5.13.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // JUnit 4 compatibility
    testImplementation("junit:junit:4.13.2")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // JUnit 5 extensions (latest: 2.3.0)
    testImplementation 'org.junit-pioneer:junit-pioneer:+'

    // Mocks, https://site.mockito.org/
    testImplementation "org.mockito:mockito-core:5.+"

    // Http Client (by default it will use the JDK HttpClient without JSON parsing)
    implementation 'io.github.openfeign:feign-core:13.6'

    // e2e, Test web server for HTTP tests
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    // JSON Schema validation (used for JSON validation and contract testing)
    testImplementation 'com.github.erosb:everit-json-schema:1.14.6'

    // AssertJ for fluent assertions
    testImplementation 'org.assertj:assertj-core:+'
}

test {
    dependsOn ':jacoco-gosu-filter:copyAgentJar' // Ensure the agent JAR is copied before tests

    def filterAgentPath = file("${rootProject.projectDir}/agents/gosu-filter-agent.jar").absolutePath

    doFirst {
        jvmArgs "-javaagent:${filterAgentPath}"

        println "JVM Args for test:"
        jvmArgs.each { println "  ${it}" }
    }

    useJUnitPlatform()

    jacoco {
        enabled = true
        includes = ['**/*']
    }

    testLogging {
        events("failed")
        showStandardStreams = true // show STDOUT and STDERR in the terminal output
    }

    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    dependsOn test // Ensure tests are run before generating the report
}

// Gosu Configuration
compileGosu {
    // TBD: Gosu compiler options can be set here
    gosuOptions.verbose = true

    // include Java classpath in Gosu compilation, so we can reference Java classes in Gosu Code
    // classpath += files(tasks.named('compileJava').get().destinationDir)
}

compileTestGosu {
    // TBD: Gosu test compiler options can be set here

    // include Java test classes into Gosu test compilation, so we can reference Java classes in Gosu Test Code
    // classpath += files(tasks.named('compileTestJava').get().destinationDir)
}

gosudoc {
    // TBD: Gosu documentation options can be set here
}

// Configure IDEA to recognize Gosu source directories
idea {
    module {
        sourceDirs += file('src/main/gosu')
        testSources.from(file('src/test/gosu'))
    }
}

jacocoTestReport {
    dependsOn test // Ensure tests are run before generating the report

    doLast {
        println ""
        println "=" * 80
        println "JaCoCo Report Generated:"
        println "  HTML: file:///${reports.html.outputLocation.get()}/index.html"
        println "  CSV:  file:///${reports.csv.outputLocation.get()}"
        println "  XML:  file:///${reports.xml.outputLocation.get()}"
        println "=" * 80
        println ""
    }
}
task viewReport {
    dependsOn jacocoTestReport
    doLast {
        def reportFile = file("${buildDir}/reports/jacoco/test/html/index.html")
        if (reportFile.exists()) {
            println "\n" + "=" * 80
            println "Opening JaCoCo HTML Report..."
            println reportFile.absolutePath
            println "=" * 80 + "\n"

            // Try to open browser (platform-specific)
            def os = System.getProperty('os.name').toLowerCase()
            if (os.contains('windows')) {
                exec { commandLine 'cmd', '/c', "start ${reportFile.absolutePath}" }
            } else if (os.contains('mac')) {
                exec { commandLine 'open', reportFile.absolutePath }
            } else {
                exec { commandLine 'xdg-open', reportFile.absolutePath }
            }
        }
    }
}

task compareCoverage {
    description = 'Shows branch coverage summary'
    dependsOn jacocoTestReport

    doLast {
        def csvFile = file("${buildDir}/reports/jacoco/test/jacocoTestReport.csv")
        if (csvFile.exists()) {
            println ""
            println "=" * 80
            println "BRANCH COVERAGE SUMMARY"
            println "=" * 80

            csvFile.eachLine { line ->
                if (line.contains('PolicyPeriodEnhancement') && !line.startsWith('GROUP')) {
                    def parts = line.split(',')
                    if (parts.length >= 12) {
                        def method = parts[3]
                        def branchMissed = parts[9]
                        def branchCovered = parts[10]
                        def branchTotal = (branchMissed as int) + (branchCovered as int)

                        if (branchTotal > 0) {
                            def coverage = ((branchCovered as int) / branchTotal * 100) as int
                            printf("%-50s | %2d branches | %3d%% covered\n",
                                    method.take(50), branchTotal, coverage)
                        }
                    }
                }
            }
            println "=" * 80
            println ""
        }
    }
}