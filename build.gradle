plugins {
    id 'java'
    id 'jacoco'
    id 'org.gosu-lang.gosu' version '+'
    id 'idea'
}

// helper scripts
apply from: "$rootDir/gradle/task-graph-publisher.gradle"
apply from: "$rootDir/gradle/offline-local-maven-repo.gradle"
apply from: "$rootDir/gradle/test-custom-terminal-reporter.gradle"
apply from: "$rootDir/gradle/helpers.gradle"

println "âœ… Using local Maven repository at: $localRepoPath"

repositories {
    maven { url project.file(localRepoPath) }
    mavenCentral()
}

group = 'org.example'
version = '1.0-SNAPSHOT'
description = 'Gosu Junit Gradle Project Example'

// force Java 11 for project code (stay close to Guidewire environment)
ext {
    JdkVersion = 11
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(JdkVersion)
        //vendor.set(JvmVendorSpec.AMAZON)
    }
    targetCompatibility = JdkVersion
    sourceCompatibility = JdkVersion
}
compileJava.options.release = JdkVersion

// Force Specific JaCoCo Version with Gosu Support
ext {
    jacocoVersion = "0.8.14-SNAPSHOT+gosu.1"
}
jacoco {
    // our custom JaCoCo build with Gosu support
    toolVersion = "${jacocoVersion}"
}

dependencies {
    // force resolution of the custom version over the local offline repository
    jacocoAnt "org.jacoco:org.jacoco.ant:${jacoco.toolVersion}"
    jacocoAgent "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}"
}

// Include Gosu source sets Into Compilation Process
sourceSets {
    main {
        gosu {
            srcDirs = ['src/main/gosu']
            filter.include '**/*.gs', '**/*.gsx', '**/*.gst', '**/*.gsp'
            filter.excludes = []
        }
    }
    test {
        gosu {
            srcDirs = ['src/test/gosu']
            filter.include '**/*.gs', '**/*.gsx', '**/*.gst', '**/*.gsp'
            filter.excludes = []
        }
    }
}

dependencies {
    // Gosu depends on Manifold
    implementation 'systems.manifold:manifold:+'

    // Gosu Language dependencies
    implementation('org.gosu-lang.gosu:gosu-core:+')
    testImplementation('org.gosu-lang.gosu:gosu-test:+')

    // JUnit 5 dependencies
    testImplementation platform('org.junit:junit-bom:5.13.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // JUnit 4 compatibility
    testImplementation("junit:junit:4.13.2")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // JUnit 5 extensions (latest: 2.3.0)
    testImplementation 'org.junit-pioneer:junit-pioneer:+'

    // Mocks, https://site.mockito.org/
    testImplementation "org.mockito:mockito-core:5.+"

    // Http Client (by default it will use the JDK HttpClient without JSON parsing)
    implementation 'io.github.openfeign:feign-core:13.6'

    // e2e, Test web server for HTTP tests
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    // JSON Schema validation (used for JSON validation and contract testing)
    testImplementation 'com.github.erosb:everit-json-schema:1.14.6'

    // AssertJ for fluent assertions
    testImplementation 'org.assertj:assertj-core:+'
}

test {
    useJUnitPlatform()

    jacoco {
        enabled = true
        includes = ['**/*']
    }

    testLogging {
        events("failed")
        showStandardStreams = true // show STDOUT and STDERR in the terminal output
    }

    finalizedBy(jacocoTestReport)
}

// Gosu Configuration
compileGosu {
    // TBD: Gosu compiler options can be set here
    gosuOptions.verbose = true

    // include Java classpath in Gosu compilation, so we can reference Java classes in Gosu Code
    // classpath += files(tasks.named('compileJava').get().destinationDir)
}

compileTestGosu {
    // TBD: Gosu test compiler options can be set here

    // include Java test classes into Gosu test compilation, so we can reference Java classes in Gosu Test Code
    // classpath += files(tasks.named('compileTestJava').get().destinationDir)
}

gosudoc {
    // TBD: Gosu documentation options can be set here
}

// Configure IDEA to recognize Gosu source directories
idea {
    module {
        sourceDirs += file('src/main/gosu')
        testSources.from(file('src/test/gosu'))
    }
}

jacocoTestReport {
    dependsOn test // Ensure tests are run before generating the report
}

tasks.register('viewReport') {
    //dependsOn jacocoTestReport
    doLast {
        def reportFile = file("${buildDir}/reports/jacoco/test/html/index.html")
        if (reportFile.exists()) {
            println "\n" + "=" * 80
            println "Opening JaCoCo HTML Report..."
            println reportFile.absolutePath
            println "=" * 80 + "\n"

            // Try to open browser (platform-specific)
            def os = System.getProperty('os.name').toLowerCase()
            if (os.contains('windows') || isWSL2()) {
                def windowsPath = reportFile.absolutePath
                // Convert WSL path to Windows-compatible path
                if (isWSL2()) {
                    windowsPath = convertWSLPathToWindows(reportFile.absolutePath)
                }
                // Use full Windows path to cmd.exe when in WSL2
                def cmdPath = isWSL2() ? '/mnt/c/Windows/System32/cmd.exe' : 'cmd'
                exec { commandLine cmdPath, '/c', "start file://${windowsPath}" }
            } else if (os.contains('mac')) {
                exec { commandLine 'open', reportFile.absolutePath }
            } else {
                exec { commandLine 'xdg-open', reportFile.absolutePath }
            }
        }
    }
}

static def isWSL2() {
    try {
        def versionFile = new File('/proc/version')
        if (versionFile.exists()) {
            def content = versionFile.text.toLowerCase()
            return content.contains('wsl') || content.contains('microsoft')
        }
    } catch (Exception e) {
        // Ignore exceptions
    }
    return false
}

static def convertWSLPathToWindows(linuxPath) {
    try {
        // Try using wslpath command for proper Windows path resolution
        def process = ['wslpath', '-w', linuxPath].execute()
        def output = process.text.trim()
        if (process.waitFor() == 0 && !output.empty) {
            return output
        }
    } catch (Exception e) {
        // wslpath not available or failed, fall back to UNC format
    }

    // Fallback: Use WSL2 UNC path format for Windows browser access
    // Remove leading slash and convert forward slashes to backslashes
    def wslPath = linuxPath.substring(1).replace('/', '\\')
    return "\\\\wsl.localhost\\Ubuntu\\${wslPath}"
}
